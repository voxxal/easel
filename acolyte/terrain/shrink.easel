// This is the maximum number of players each map is designed to hold
// Smaller than this and we will quickly shrink to the proportional area (not radius) for that number of players
const ShrinkMaxPlayers = 8
const ShrinkRatePerTick = 0.003 // This is the rate at which the turf will shrink to its target size
const ShrinkPower = 0.5 // Reshape the shrink curve according to this power - less than 1 means the shrinking is slower at start and faster at end, the opposite for greater than 1

field scene.ShrinkStartTick
pub prop scene.ShrinkScale

pub behavior fn scene.Shrink(shrinkDuration=90s) {
	ShrinkScale ??= 1.0

	if !HasCommenced {
		await AfterCommence
	}

	let startTick = ShrinkStartTick ??= Tick
	loop {
		await TickStart
		if HasConcluded { break } // Stop shrinking once game is complete

		const decayOverTime = 1.0 - ((Tick - startTick) / shrinkDuration).Max(0.0)
		const decayOverNumPlayers = ((QueryUnits.WhereCategory(Category:Hero).Count / ShrinkMaxPlayers) ** 0.5).Min(1.0) // 0.5 power to square root because we want to prorate area not radius
		const target = (decayOverTime * decayOverNumPlayers) ** ShrinkPower

		ShrinkScale = ShrinkRatePerTick.Mix(ShrinkScale, target)
	}
}