use owner=null

pub fn obstacle.Boulder(shape, pos=@(0,0), heading=0, maxHp=50.0, radius=1.0, density=50, turningDecay=0.05, [posOffset?, headingOffset?]) {
    use body=obstacle, life=obstacle
	use color=#bbb, fade=#822,
	use layer=Layer:Obstacle

	Body(pos = pos + posOffset, heading = heading + headingOffset)
	Durability = Durability:Maximum
	DecaySpeed
	DecayTurnRate

	Health(maxHp)
	LavaBehavior

	PolygonCollider(
        category = Category:Obstacle | Category:Grabbable | Category:Aoe,
	)
	PolygonPerimeter

	Anchored
	once AfterGameCommenced { delete Anchored }

	with Hp {
		await Paint // coalesce multiple changes to HP into a single paint at the end of tick
		use color = HpProportion.Mix(fade, color)
		PolygonSprite(shading=0.25, shadow=0.5)
	}

	once BeforeOutOfHealth {
		repeat 10 {
			Spark(shadow=0, luminous=1, feather=1, shine=1, bloom=1, dissipate=30, speed=10, splatter=1, glaze=true)
		}
	}
}