use owner=null

pub fn obstacle.Mirror(shape, pos=@(0,0), heading=0, maxHp=50, radius=1.0, density=100, [posOffset?, headingOffset?]) {
    use body=obstacle, life=obstacle
	use color=#0cf, fade=#48f,
	use bloom=0.5, luminous=1,
	use shading=0.25, layer=Layer:Obstacle,

	Body(pos = pos + posOffset, heading = heading + headingOffset)
	Durability = Durability:Reflector
	DecaySpeed
	DecayTurnRate(turningDecay=0.05)

	Anchored
	once AfterGameCommenced { delete Anchored }

	Health
	LavaBehavior

	PolygonCollider(
        category = Category:Obstacle | Category:Shield | Category:Grabbable | Category:Aoe,
        collideWith=Category:Tangible,
	)
	PolygonPerimeter

	with Hp {
		await Paint // coalesce multiple changes to HP into a single paint at the end of tick
		use color = HpProportion.Mix(fade, color)
		PolygonSprite(shadow=0.5)
	}

	once BeforeOutOfHealth {
		repeat 10 {
			Spark(shadow=0, feather=1, luminous=1, shine=1, bloom=1, dissipate=30, speed=10, splatter=1, glaze=true)
		}
	}
}