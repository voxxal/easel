use owner=null

pub fn obstacle.Volcano(shape, pos=@(0,0), heading=0, maxHp=50, owner?, damage=36, radius=1.0, [posOffset?, headingOffset?]) {
    use body=obstacle, life=obstacle
	use damageInterval = 5
	use color=#f08e, fade=#f084
	use bloom=2, bloomAlpha=0.5, luminous=0
	use layer=Layer:Zone

	Body(pos = pos + posOffset, heading = heading + headingOffset, immovable=true)
	Durability = Durability:Maximum
	Health
	LavaBehavior

	SpawnContact<volcano>(filter=Category:Unit) unit {
		use body = unit

        on Paint(interval=4) {
            if Invisibility {
                await Invisibility
            } else {
                Spark(
                    color=, fade=1, shine=0.5, luminous=1, feather=1, bloom=0,
                    radius=1.0, dissipate=15, layer=Layer:Spark,
                    glaze=1, speed=8)
            }
        }

		with Tick(damageInterval) {
			unit.TakeDamage(damageInterval * damage / TicksPerSecond)
		}
	}

	PolygonCollider(
        category=Category:Zone,
        collideWith=Category:None, sense=Category:Unit,
        isSensor=true,
	)
	PolygonPerimeter

	with Hp {
		await Paint // coalesce multiple changes to HP into a single paint at the end of tick

		use color = HpProportion.Mix(fade, color)

		PolygonSprite
		
		on Paint(8) {
			Spark(
				color=, fade=#0000, luminous=0,
				shine=0, bloom=0, shadow=0, feather=1,
				dissipate=30, speed=8,
				glaze=true, layer=Layer:Buff)
		}
	}

	once BeforeOutOfHealth {
		repeat 10 {
			Spark(
				shadow=0, feather=1,
				luminous=1, shine=1, bloom=2,
				dissipate=30, speed=10,
				glaze=true)
		}
	}
}