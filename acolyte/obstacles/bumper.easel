use owner=null

pub fn obstacle.Bumper(shape, pos=@(0,0), heading=0, maxHp=50, impulse=200, radius=1, density=50, [posOffset?, headingOffset?]) {
    use body=obstacle, life=obstacle
	use color=#c94, fade=#753,
	use bloom=0, luminous=0,
	use shading=0.25, layer=Layer:Obstacle,
	use restitution=1

	Body(pos = pos + posOffset, heading = heading + headingOffset)
	Durability = Durability:Maximum
	DecaySpeed
	DecayTurnRate(turningDecay=0.05)

	Health(maxHp)
	LavaBehavior

	Anchored
	once AfterGameCommenced { delete Anchored }

	PolygonCollider(
        category=(Category:Obstacle | Category:Grabbable | Category:Aoe),
        restitution=1,
    )
	PolygonPerimeter

	with Hp {
		await Paint // coalesce multiple changes to HP into a single paint at the end of tick
		use color = HpProportion.Mix(fade, color)

		PolygonSprite(shadow=0.5)
	}

	on BeforeCollide that {
		if that.Category.Overlaps(Category:Unit | Category:Obstacle) {
			that.ApplyImpulse(impulse * Direction(that.Pos - this.Pos))
			Strobe(dissipate=10, shine=0.5, growth=0.5)
			Hear(@bumper.esfx)
		}
	}

	once BeforeOutOfHealth {
		repeat 10 {
			Spark(shadow=0, luminous=1, feather=1, shine=1, bloom=1, dissipate=30, speed=10, splatter=1, glaze=true)
		}
	}
}