.. glyph=TripleScratches:Glyph, color=#ff0088
.. damage=24.0, numBolts=3, buffDuration=4s
.. lifesteal=0.3, cooldown=1.5s

pub symbol WantTrifire

pub fn TrifireOption(.. ui) {
	SlotOption<WantTrifire> {
		H1 { %("Trifire") }

		P {
			%("Summon a trinity of scorching flames upon your enemies, engulfing them in a long-lasting burn. The bolts spread outwards, making it challenging for foes to avoid from afar, while up close, the combined power of all three overwhelms even the toughest of adversaries.")
		}

		P {
			SlotEffect("Stacking Burn", icon="fas fa-flame") {
				const boltDps = damage / (buffDuration / TicksPerSecond) / numBolts
				%("Each bolt of Trifire will add another stack of " + boltDps + " damage per second to your enemy. ")
				%("You must hit them with Trifire at least once every " + (buffDuration / TicksPerSecond) + " seconds to keep the fire burning. ")
			}
		}

		SlotStats {
			SlotStat("Damage", icon="fas fa-sword") { %(damage) }
			SlotStat("Lifesteal", icon="fas fa-heart") { %((lifesteal * 100.0) + "%") }
			SlotStat("Cooldown", icon="fas fa-clock") { %((cooldown / TicksPerSecond) + "s") }
		}
	}
}

pub fn ability.TrifireAbility(unit, owner, btn) {
	on BotAttacking {
		if Cooldown == 0 {
			BotUseAbility
		}
	}

	Incantation {
		.. damage = damage * unit.DamageModifier

		await Spray(duration=0, count=numBolts, spray=0.09revs) headingOffset index {
			.. damage = damage / count

			Spawn projectile {
				.. parent=unit
				.. lifetime=1s, dissipation=10
				.. radius = index <= 0 ? 0.22 : 0.15
				.. speed = index <= 0 ? 48 : 32
				.. density=40, ccd=true
				.. durability = index <= 0 ? Durability:Medium : Durability:Low
				.. luminous=1.0, depth=Depth:Projectile
				.. categories=Category:Projectile, collideWith=Category:Solid

				LaunchBody(headingOffset=, speed=24)
				PrepareForCollision
				PropulsiveMotion(0.03)
				Sing(Trifire:Sound)
				ExpireAfterLifetime

				Collider {
					Shape:Circle
				}

				RayEmitter(dissipation=8)

				let canHurtSelf = false
				on BeforeCollide that {
					if canHurtSelf || that.Team != owner.Team {
						Attack(that, damage=0) // All damage is done by Poison, not this - this is to do other things like interrupt the enemy
						if that.HasHp {
							that.StackBuff<burning>(cleansable=true, maxStacks=that.Category & Category:Hero ? 99 : 0) stacks {
								ExpireAfterLifetime(buffDuration)
								Poison(that, damage * (stacks + 1), buffDuration)
								Smoke(that,
									radius=1.0, depth=Depth:Particle,
									color=color.WithOpacity(0.1 + stacks * 0.07), fade=1,
									flutter=$perimeter, splatter=Splatter:Outwards(12))

								that.Tombstone victim {
									NameBlock(owner)
									%(" ignited ")
									NameBlock(victim)
									if stacks > 0 {
										%(" and watched them burn ")
									}
								}
							}
						}
					}
				}
				on AfterCollide that {
					if that.Category & Category:Shield {
						canHurtSelf = true // Can bounce off shield to hurt self
					}
					if that.Durability >= this.Durability {
						Strobe(shine=1.0)
						Puff(count=5, splatter=1, dissipation=30)
						Hear(Trifire:HitSound)
						Expire
					}
				}
			}
		}
	}
}

sound Trifire:Sound(duration=1s) {
	BrownNoise
	BandPass(freq=735, q=[5,15])
	Envelope(attack=0.25s, sustain=0s)
	Gain(volume=0.33)
}

sound Trifire:HitSound(duration=0.33s) {
	Oscillator(wave=$square, freq=[100,0], harmonics=[1,1.1,1.2,1.3,1.4,1.5,1.6,1.7,1.8,1.9,2])
	LowPass(freq=250)
	Envelope(attack=0.01s, decay=0.1s, sustainVolume=0.3, sustain=0s)
}