use glyph=@sprint.svg, color=#8800ff, lifetime=3.0s, cooldown=10.0s, fastProportion=0.8

pub symbol WantVoidRush

pub fn VoidRushDescription([ui]) {
    H1 { "Void Rush" }

    P {
        "Tap into the dark energies of the void to quicken your movements while also rendering yourself immune to its malevolent effects."
    }

    SlotEffect("Void Surge", icon="fas fa-running") {
        "Move " + (fastProportion * 100) + "% faster for " + (lifetime / TicksPerSecond) + " seconds. "
    }
    SlotEffect("Void Immunity", icon="fas fa-shield-heart") {
        "For the duration of this spell, you are immune to damage from the void. "
    }

    Blank(expand=true)

    SlotStats {
        SlotStat("Cooldown", icon="fas fa-clock") { %((cooldown / TicksPerSecond) + "s") }
    }
}

pub fn VoidRushOption([ui]) {
	SlotOption(WantVoidRush) {
        VoidRushDescription
	}
}

pub fn ability.VoidRushAbility([unit, owner, keycode, btnEdit?, btnOrder?, btnSize?]) {
    use body=unit, life=unit

	with IsInLava {
		CooldownRate(IsInLava ? 2 : 1)
	}

	on BotRecovering target {
		if Cooldown == 0 {
			BotUseAbility
		}
	}

    AbilityCommandBtn {
        VoidRushDescription
    }

	SpawnEachIncantation incantation {
		use dissipate=60, ownerColor=true, layer=Layer:Projectile
		use shine=0.3, bloom=1.5, luminous=1

		CleanseEffects
		UngrabMe

		unit.SpawnEffect {
			LavaDamageModifier(0)
			Sing(@voidRush.esfx)
			CooldownRate(0)
			SpeedModifier(1 + fastProportion)

			await Tween(duration=lifetime, initial=1.0, final=0.0) factor {
				on Paint {
					Spark(
						radius=1.0, layer=Layer:Buff,
						color=color.WithOpacity(factor), fade=1,
						feather=0, shine=0.5, shadow=0.25,
						speed=5, splatter=1)
				}
			}

			Expire
		}
	}
}