use glyph=@aura.svg, color=#ff4400

pub symbol WantReverberate

pub fn ReverberateDescription([ui]) {
    H1 { "Reverberate" }

    P {
        Span(italic=true) { "Passive ability. " }
        "Imbue every atom in your body with an invisible, yet powerful, sonic resonance. Even the slightest contact with your vibrating form sends enemies reeling, forcefully repelled by the resounding shockwave that pulsates from your very being."
    }
}

pub fn ReverberateOption([ui]) {
	SlotOption(WantReverberate) {
        ReverberateDescription
	}
}

pub fn ability.ReverberateAbility([unit, owner, keycode, btnEdit?, btnOrder?, btnSize?]) {
    use body=unit, life=unit

    AbilityCommandBtn(passive=true) {
        ReverberateDescription
    }

	NoIncantation {
		on unit.BeforeCollide that {
            // Don't apply reverberate if we are intangible, e.g. because of Blaze
			if unit.Tangibility.Overlaps(Category:Unit) && that.Category.Overlaps(Category:Unit) && that.Team != owner.Team {
				ImpulseAway(that, impulse=100)
				Hear(@reverberate.esfx)

				that.Tombstone victim {
					PlayerNameDisplay(owner)
					" reverberated "
					PlayerNameDisplay(victim)
					" into unending silence "
				}
			}
		}
	}
}