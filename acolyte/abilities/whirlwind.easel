.. glyph=Snowflake1:Glyph, color=#44ffff, cooldown=7.5s, slowProportion=0.5, buffDuration=2.0s

pub symbol WantWhirlwind

pub fn WhirlwindOption(.. ui) {
	SlotOption<WantWhirlwind> {
		H1 { %("Whirlwind") }

		P {
			%("Summon a bone-chilling vortex that engulfs your enemies, impeding their movements with its icy grip while blowing away incoming attacks with the might of the winds.")
		}

		P {
			SlotEffect("Chilling Blow", icon="fas fa-snowflake") {
				%("Enemies caught in your whirlwind will be slowed " + (slowProportion * 100) + "% for " + (buffDuration / TicksPerSecond) + " seconds.")
			}
		}

		SlotStats {
			SlotStat("Cooldown", icon="fas fa-clock") { %((cooldown / TicksPerSecond) + "s") }
		}
	}
}

pub fn ability.WhirlwindAbility(unit, owner, btn) {
	on BotAttacking {
		if Cooldown == 0 {
			BotUseAbility
		}
	}

	Incantation {
		TurnRateModifier(-1.0)

		await ChargeForDuration(6) {
			ChargingIndicator
		}

		Spawn projectile {
			.. parent=unit
			.. lifetime=2s, dissipation=20 
			.. radius=2.4, density=1, speed=12.0
			.. luminous=0, glare=0.7, glareAlpha=0.4, bloom=0, feather=0.5, depth=Depth:Projectile
			.. categories=Category:Projectile, collideWith=Category:None, sense=Category:Hero

			ExpireAfterLifetime
			LaunchBody
			PrepareForCollision
			PropulsiveMotion
			Sing(Whirlwind:Sound)
			Bloom(bloom=4, luminous=0.5)
			Smoke(
				count=2, radius=1.2, splatter=1.3,
				color=#4ff4, fade=1,
				flutter=Flutter:Swirl(radius=0.8, period=13))

			Collider {
				Shape:Circle
			}

			on BeforeCollide that {
				if (that.Category & Category:Hero) && that.Team != owner.Team {
					// spawn buff with separate lifetime from projectile
					that.ReplaceBuff<cold>(cleansable=true) {
						ExpireAfterLifetime(buffDuration)
						SpeedModifier(-slowProportion, that)
						Smoke(that,
							color=#4ff4, fade=1, feather=0.5,
							radius=1.0, dissipation=20, depth=Depth:Particle,
							flutter=$perimeter)

						that.Tombstone victim {
							NameBlock(owner)
							%(" took ")
							NameBlock(victim)
							%("'s breath away")
						}
					}

					Strobe(shine=1.0, growth=0.1)
				}
			}

			Wind(0.3, radius=)
		}
	}
}

sound Whirlwind:Sound(duration=5s, cutoff=0.25s) {
	BrownNoise
	BandPass(freq=240, q=0.7)
	LowShelf(freq=80, db=12)
	Envelope(attack=0.5s, sustain=0s)
	Gain(volume=0.25)
}