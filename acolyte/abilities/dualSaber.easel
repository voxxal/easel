use glyph=@waveStrike.svg, color=#ff0000, damage=0, cooldown=7.5s, lifetime=1.5s, length=5.5

pub symbol WantDualSaber

pub fn DualSaberDescription([ui]) {
    H1 { "Dualsaber" }

    P {
        "Wield formidable twin sabers forged from pure light. The incandescent blades can expertly parry incoming projectiles and forcefully knock back your enemies with each swift and decisive strike. "
    }

    SlotEffect("Dual Wield", icon="fas fa-shuffle") {
        "Though each saber may be shorter than a solitary counterpart, their combined presence doubles the odds of delivering a decisive strike against your adversaries."
    }

    Blank(expand=true)

    SlotStats {
        SlotStat("Cooldown", icon="fas fa-clock") { %((cooldown / TicksPerSecond) + "s") }
    }
}

pub fn DualSaberOption([ui]) {
	SlotOption(WantDualSaber) {
        DualSaberDescription
	}
}

pub fn ability.DualSaberAbility([unit, owner, keycode, btnEdit?, btnOrder?, btnSize?]) {
    use body=unit, life=unit

	on BotAttacking target {
		if Cooldown == 0 && Distance(target, unit.Pos) <= length {
			BotUseAbility
		}
	}

    AbilityCommandBtn {
        DualSaberDescription
    }

	SpawnEachIncantation incantation {
        UngrabMe
        UngrabThem

		on Interrupt interruptor {
			if interruptor.Overlaps(Interruptor:Self) {
				incantation.Expire
			}
		}

		on BotChannelling target {
			if Distance(target, unit.Pos) <= length {
				BotKeepChannelling(spin=true)
			}
		}

		Subspawn saber {
			use parent=unit, body=saber
			use dissipate=10, luminous=1
			use radius=0.25, angleOffset=0.25rev
			use restitution=1
			use category=Category:Shield
			use layer=Layer:Saber
			use fade=1, shadow=0.25, luminous=1, glare=1, glareAlpha=1, bloom=4, bloomAlpha=1
			use shape = Capsule(extent=length, pos=@(0,0), angle=angleOffset)

			Durability = Durability:Reflector
			Movement:SynchronizedBody(unit, bullet=true)
			Sing(@dualSaber.esfx)
			SpeedModifier(0.75)
			TurnRateModifier(0.05)

			// Main saber collider is actually attached to the unit's body, which means it will transmit forces back onto the unit (e.g. recoil)
			PolygonCollider(body=unit, collideWith=Category:Unit, density=1.25)

			// Deflector is attached to the saber's independent body so projectiles cannot knock the unit back through the saber
			PolygonCollider<deflector>(collideWith=Category:Projectile, density=5.0, isolate=true)
			
			// Sprite is attached to the unit (not the independent deflector body) so it does not visually lag behind
			PolygonSprite(body=unit, shadow=)

			on Paint {
				Swoop(length=, layer=Layer:Bloom, angleOffset=)
				Swoop(length=, layer=Layer:Bloom, angleOffset = -angleOffset)
			}

			on Hurt {
				Strobe(dissipate=10, shine=1.0)
			}

			on BeforeCollide that { // Main collider hits units
				Hear(@dualSaberHit.esfx)

				if that.Team != owner.Team {
					Attack(that)

					that.Tombstone victim {
						PlayerNameDisplay(owner)
						" slashed "
						PlayerNameDisplay(victim)
						" into bite-sized pieces "
					}

					await Tick(0.25s)
					incantation.Expire
				}
			}

			on BeforeCollide<deflector> that { // Secondary collider hits projectiles only
				Hear(@dualSaberHit.esfx)
				if that.Category.Overlaps(Category:Destructible) { that.Expire }
			}
		}

		await Tick(lifetime)
	}
}