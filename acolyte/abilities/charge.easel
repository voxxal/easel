use glyph=@fireDash.svg, color=#ff00cc, damage=0, speed=120, cooldown=10.0s

pub symbol WantCharge

pub fn ChargeDescription([ui]) {
    H1 { "Charge" }

    P {
        "Charge forward with thunderous force, knocking away anything in your path."
    }

    Blank(expand=true)

    SlotStats {
        SlotStat("Cooldown", icon="fas fa-clock") { %((cooldown / TicksPerSecond) + "s") }
    }
}

pub fn ChargeOption([ui]) {
	SlotOption(WantCharge) {
        ChargeDescription
	}
}

pub fn ability.ChargeAbility([unit, owner, keycode, btnEdit?, btnOrder?, btnSize?]) {
    use body=unit, life=unit

	with IsInLava {
		CooldownRate(IsInLava ? 2 : 1)
	}

	on BotRecovering target {
		if Cooldown == 0 {
			BotUseAbility
		}
	}

    AbilityCommandBtn {
        ChargeDescription
    }

	SpawnEachIncantation incantation {
		use lifetime=16
		use radius=1.5, dissipate=12
		use ownerColor=true, layer=Layer:Projectile
		use shine=1.0, glare=0.7, bloom=3.0, luminous=1

		CleanseEffects
		UngrabMe
		Sing(@charge.esfx)

		// Don't allow other movement to interfere with the charge
		SpeedModifier(0)
		DensityModifier(140)
		SizeModifier(1.5)
		Thrust(Pointer, speed=)

		once Arrived(Pointer) {
			incantation.Expire
		}

		on Paint {
			Streak(fade=1, bloom=3, bloomAlpha=1, glare=1, shadow=0.5)
		}

		on unit.BeforeCollide that {
			Attack(that)

			that.Tombstone victim {
				PlayerNameDisplay(owner)
				" smashed "
				PlayerNameDisplay(victim)
				" with thunderous force "
			}
		}

		await Tick(lifetime)
	}
}