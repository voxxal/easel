.. glyph=MineExplosion:Glyph, color=#ff009c, damage=12.0, lifesteal=0.3, cooldown=5.0s, armDelay=7, retreatDelay=1s

pub symbol WantEnergyMines

pub fn EnergyMinesOption(.. ui) {
	SlotOption<WantEnergyMines> {
		H1 { %("Energy Mines") }

		P {
			%("Mark your territory with solid ingots of volatile energy, protecting you from all who dare come too close.")
		}

		P {
			SlotEffect("Stabilizing Presence", icon="fas fa-anchor") {
				%("Your sustained presence stabilizes the mines' volatile energy. Stray too far and they will expire.")
			}
			SlotEffect("Arming Delay", icon="fas fa-stopwatch") {
				%("Energy mines take a moment to activate and only do damage after " + (armDelay / TicksPerSecond) + "s.")
			}
		}

		SlotStats {
			SlotStat("Damage", icon="fas fa-sword") { %(damage) }
			SlotStat("Lifesteal", icon="fas fa-heart") { %((lifesteal * 100.0) + "%") }
			SlotStat("Cooldown", icon="fas fa-clock") { %((cooldown / TicksPerSecond) + "s") }
		}
	}
}

pub fn ability.EnergyMinesAbility(unit, owner, btn) {
	on BotAttacking {
		if Cooldown == 0 {
			BotUseAbility
		}
	}

	Incantation {
		.. damage = damage * unit.DamageModifier
	
		TurnRateModifier(-1)
		SpeedModifier(-1)

		Hear(Mines:LaunchSound)
		await Spray(duration=5, count=5, spray=0.28revs) {
			Spawn projectile {
				.. parent=unit
				.. lifetime=4.5s, dissipation=15
				.. radius=0.32, speed=40.0, impulse=75, aoe=1.2
				.. density=1.5, ccd=true
				.. ownerColor=Alliance:Self, bloom=1.5, bloomAlpha=0.3, luminous=1.0, depth=Depth:Projectile
				.. categories = Category:Projectile | Category:Aoe | Category:Destructible | Category:AlwaysBlow, durability=Durability:None
				.. sense=Category:Projectile

				LaunchBody
				PrepareForCollision
				DriftingMotion
				ExpireAfterLifetime

				Sprite(shadow=0.5) {
					Collider {
						Shape:Circle
					}
				}

				let armed = false
				on BeforeCollide that {
					if that.Durability >= this.Durability && !(that.Owner == this.Owner && !(that.Category & Category:Hero)) {
						// Expire on everything unless it is an entity owned by the same player but not the hero.
						// This lets us push mines around with meteors, for example.
						Strobe(shine=1.0)
						Expire
					}
				}

				once Expire {
					Explosion(1.2, shine=1.0)
					Hear(Mines:HitSound)

					Aoe:ForEach that {
						if that.Team != owner.Team {
							Attack(that, armed ? damage : 1)
							Aoe:Impulse

							that.Tombstone victim {
								NameBlock(owner)
								%(" blew ")
								NameBlock(victim)
								%("'s mined")
							}
						}
					}
				}

				once<arm> Sleep(armDelay) {
					armed = true
					Speed = 0
					DecayingMotion
				}

				once<retreat> Sleep(retreatDelay) {
					Proximity:OnRetreat(unit, range=12) {
						Expire
					}
				}
			}
		}
	}
}

sound Mines:LaunchSound(duration=0.17s) {
	Vibrato(freqModulation=[3000,12588]) {
		Oscillator(freq=30)
	}
	Oscillator(wave=$triangle, freq=[6000,6400], harmonics=[1, 1.33, 1.5])
	Tremolo(volumeModulation=0.2) {
		Oscillator(freq=30)
	}
	Envelope(attack=0.05s, release=0.05s)
	Gain(volume=0.1)
}

sound Mines:HitSound(duration=1s) {
	Oscillator(wave=$triangle, freq=[50,0], harmonics=[1,1.1,1.2,1.3,1.4,1.5,1.6,1.7,1.8,1.9,2])
	LowPass(freq=[300,0])
	Envelope(attack=0.01s, sustain=0s)
}